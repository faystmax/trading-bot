plugins {
    id 'base'
    id "com.moowork.node" version "1.3.1"
}

npm_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}
assemble.dependsOn npm_run_build

task test(type: NpmTask) {
    dependsOn assemble

    // force Jest test runner to execute tests once
    // and finish the process instead of starting watch mode
    environment CI: 'true'

    args = ['run', 'test']

    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
}
build.dependsOn test

task deploy {
    doLast {
        ssh.run {
            session(remotes.server) {
                executeSudo "rm -rf ./trading-bot-ui", pty: true
                executeSudo "mkdir ./trading-bot-ui", pty: true
                put from: "${projectDir}/build/", into: "./trading-bot-ui"
                executeSudo "cp -r ./trading-bot-ui/build/. /srv/trading-bot-ui/", pty: true
            }
        }
    }
}
deploy.dependsOn build
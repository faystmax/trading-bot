plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

springBoot {
    buildInfo()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.integration:spring-integration-mail'
    implementation 'org.telegram:telegrambots-spring-boot-starter:6.3.0'
    implementation 'com.github.faystmax:exchange-rates-api-client:1.1.0'
    implementation 'com.github.faystmax:binance-api-client:1.0.4'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.mail:mail:1.4.7'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-email:1.5'

    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.6'
    testImplementation 'org.testcontainers:postgresql:1.17.6'
}

test {
    useJUnitPlatform()
}

tasks.register('deploy') {
    doLast {
        ssh.run {
            session(remotes.server) {
                put from: bootJar.archiveFile.get().asFile.absolutePath, into: "./"
                executeSudo "service trading-bot stop", pty: true
                executeSudo "cp ./${bootJar.archiveFileName.get()} /srv/trading-bot/", pty: true
                executeSudo "rm -f /srv/trading-bot/application.jar", pty: true
                executeSudo "ln -s /srv/trading-bot/${bootJar.archiveFileName.get()} /srv/trading-bot/application.jar", pty: true
                executeSudo "service trading-bot start", pty: true
            }
        }
    }
}
deploy.dependsOn build